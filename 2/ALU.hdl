// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    Not16(in = x, out = nxout);
    Mux4Way16(a= x, b= false, c= nxout, d= true, sel[1]= nx, sel[0]= zx, out= xout);
    
    Not16(in = y, out = nyout);
    Mux4Way16(a= y, b= false, c= nyout, d= true, sel[1]= ny, sel[0]= zy, out= yout);

    And16(a= xout, b= yout, out= andout);
    Not16(in= andout, out= Nandout);

    Add16(a = xout, b = yout, out = addout);
    Not16(in= addout, out= Naddout);
    
    Mux4Way16(a= andout, b= addout, c= Nandout, d= Naddout, sel[0]= f, sel[1]= no, out= out, out[15] = ng, out[0..7] = lsb, out[8..15] = msb);

    Or8Way(in= lsb, out= zrpt1);
    Or8Way(in= msb, out= zrpt2);
    Or(a= zrpt1, b= zrpt2, out= notzero);
    Not(in= notzero, out= zr);
}